buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.0'
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.application'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
}

android {
    /*******************************************************
     * Configuration de base
     *******************************************************/
    namespace 'com.coinche.game'
    compileSdkVersion 34
    buildToolsVersion '34.0.0'

    ndkVersion '26.1.10909125'

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qtAndroidDir + '/src', 'src', 'java']
            aidl.srcDirs = [qtAndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = ['res']
            resources.srcDirs = ['resources']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        minSdkVersion = 28
        targetSdkVersion = 34
        versionCode 1
        versionName "1.0.0"

        ndk {
            abiFilters qtTargetAbiList.split(",")
        }

    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    /*******************************************************
     * Configuration de signature (pour release)
     *******************************************************/
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            jniDebuggable true
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

}

// Workaround: androiddeployqt (Qt 6.9.3) inserts package="" into the manifest,
// which AGP 8.1.0 rejects. Strip it before Gradle processes the manifest.
tasks.register('fixManifestPackageAttribute') {
    doLast {
        def manifestFile = file("AndroidManifest.xml")
        if (manifestFile.exists()) {
            manifestFile.text = manifestFile.text.replace(' package=""', '')
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('process') && task.name.endsWith('MainManifest')) {
        task.dependsOn 'fixManifestPackageAttribute'
    }
}

// Copie l'APK signé avec le nom attendu par Qt Creator
afterEvaluate {
    tasks.named('assembleRelease').configure {
        doLast {
            def signedApk = file('build/outputs/apk/release/android-build-coinche-release.apk')
            def unsignedApk = file('build/outputs/apk/release/android-build-coinche-release-unsigned.apk')
            if (signedApk.exists()) {
                unsignedApk.delete()
                java.nio.file.Files.copy(signedApk.toPath(), unsignedApk.toPath())
                println("APK copié pour Qt Creator: ${unsignedApk.absolutePath}")
            }
        }
    }

    // Copie l'AAB signé avec le nom attendu par Qt Creator
    tasks.named('bundleRelease').configure {
        doLast {
            def signedAab = file('build/outputs/bundle/release/android-build-coinche-release.aab')
            def targetAab = file('build/outputs/bundle/release/coinche-release.aab')
            if (signedAab.exists()) {
                targetAab.delete()
                java.nio.file.Files.copy(signedAab.toPath(), targetAab.toPath())
                println("AAB créé pour Qt Creator: ${targetAab.absolutePath}")
                println("Chemin complet: ${targetAab.absolutePath}")
            }
        }
    }
}
